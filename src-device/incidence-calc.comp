#version 450
#extension GL_ARB_separate_shader_objects : enable

layout (constant_id = 0) const int NUM_POINTS = 200;

layout(binding = 0) uniform GlobalInput
{
	// This orientation is given as a normalized quaternion,
	// where the scalar component is w.
	vec4 to_sun_rotation;
	vec3 sun_direction;
};

layout(binding = 1) uniform sampler2D depth_map;

struct Point
{
	vec3 position;
	vec3 normal;
};

layout(binding = 2) uniform Input
{
	Point point[NUM_POINTS];
};

layout(binding = 3) buffer Output
{
	float incidence[NUM_POINTS];
};

#include "quaternion.glsl"

void main()
{
	// Tolerance to account for texture sampling interpolation
	// error (which must be set to linear, not nearest).
	const float tol = 1e-4;

	const Point p = point[gl_GlobalInvocationID.x];

	// Rotate the point to sun's standpoint:
	vec3 pos = quat_rot_vec(
		to_sun_rotation,
		p.position
	);
	pos.z = 0.5 * pos.z + 0.5;

	// Depth test
	float visible_dist = texture(depth_map, pos.xy).r;
	if(0.5 * pos.z + 0.5 > visible_dist + tol) {
		// The point is shadowed, don't compute
		// direct sunlight.
		return;
	}

	// Point is exposed, accumulate solar incidence.
	// maX() is a precaution, because points facing
	// outwards the sun should never be exposed.
	incidence[gl_GlobalInvocationID.x] +=
		max(0.0, dot(sun_direction, p.normal));
}
